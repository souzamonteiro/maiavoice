#!/usr/bin/env maiascript

///
/// @license
/// Copyright 2020 Roberto Luiz Souza Monteiro,
///                Renata Souza Barreto,
///                Hernane Borges de Barros Pereira.
///
/// Licensed under the Apache License, Version 2.0 (the "License")
/// you may not use this file except in compliance with the License.
/// You may obtain a copy of the License at
///
///   http://www.apache.org/licenses/LICENSE-2.0
///
/// Unless required by applicable law or agreed to in writing, software
/// distributed under the License is distributed on an "AS IS" BASIS,
/// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, eitherMath.express or implied.
/// See the License for the specific language governing permissions and
/// limitations under the License.
///

///
/// MaiaApp core class.
/// @namespace maiavoice
///
namespace maiavoice {
    appTitle = "MaiaVoice"
    appName = "maiavoice"
    scriptPath = "./"
    cnaLibrary = this.scriptPath + "/maia/cna/cna.maia"
    
    /// Starts the application and processes the data passed as command line arguments.
    /// @method run
    /// @memberof maiavoice
    function run() {
        // Supports only the Node.js interpreter.
        if (core.type(process) != "undefined") {
            command = "node"
            
            fs = system.require("fs")

            realPath = fs.realpathSync(system.argv[0])
            filePath = realPath.split("/")
            filePath = core.slice(filePath, 0, filePath.length - 2)
            filePath = filePath.join("/")
            this.scriptPath = filePath
            
            vosk = system.require("vosk")
            Stream = system.require("stream")
            Readable = Stream.Readable
            wav = system.require("wav")

            readTextFile = fs.readFileSync

            // Read file callback.
            function readFile(input) {
                content = readTextFile(input, "utf-8")
                return(content)
            }
            
            // Command line options.
            inputFile = ""
            outputFile = ""
            language = ""
            
            // Get command line arguments.
            if (system.argc > 1) {
                i = 1
                while (i < system.argc) {
                    if ((system.argv[i] == "-h") | (system.argv[i] == "--help")) {
                        system.log(maiavoice.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiavoice.appName + " [options] [input file] [--] [arguments]")
                        system.log("Options:")
                        system.log("-h     --help               Displays this help message.")
                        system.log("-l     --lang               Audio language.")
                        process.exit(0)
                    } elseif (system.argv[i] == "-o") {
                        i = i + 1
                        outputFile = system.argv[i]
                    } elseif (system.argv[i] == "-l") {
                        i = i + 1
                        language = system.argv[i]
                    } elseif (system.argv[i] == "--lang") {
                        i = i + 1
                        language = system.argv[i]
                    } else {
                        inputFile = system.argv[i]
                        break
                    }
                    i = i + 1
                }
                
                local Glob = system.require("glob")
                // Process each file based on glob pattern.
                function processFiles(er, files) {
                    if (files.length == 0) {
                        system.log(maiavoice.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiavoice.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        for (i = 0; i < files.length; i = i + 1) {
                            // Get the full file name.
                            file = files[i]
                            
                            // Get the file without extension.
                            fileName = file.split(".")
                            fileName = fileName.shift()

                            // Get the file name extension.
                            fileExtension = file.split(".")
                            fileExtension = fileExtension.pop()

                            modelPath = "models/" + language
                            if (!fs.existsSync(modelPath)) {
                                system.log("Please download the model from https://alphacephei.com/vosk/models and unpack as 'models/" + modelPath + "' in the '" + this.scriptPath + "' folder.")
                                process.exit()
                            }

                            vosk.setLogLevel(0)
                            model := vosk.Model(modelPath)

                            wfReader := wav.Reader()
                            wfReadable := Readable()
                            wfReadable.wrap(wfReader)

                            async onFormat(format) {
                                audioFormat = format.audioFormat
                                sampleRate = format.sampleRate
                                channels = format.channels
                                if ((audioFormat != 1) || (sampleRate != 16000) || (channels != 1)) {
                                    system.log("Audio file must be WAV format mono 16kHz PCM.")
                                    process.exit(1)
                                }
                                rec := vosk.Recognizer({"model": model, "sampleRate": sampleRate})
                                rec.setMaxAlternatives(10)
                                rec.setWords(true)

                                outputContents = []

                                async onData(data) {
                                    end_of_speech = rec.acceptWaveform(data)
                                    if (end_of_speech) {
                                        //system.log(JSON.stringify(rec.result(), null, 4))

                                        outputContents.push(rec.finalResult(rec))
                                    }
                                }

                                async onEnd(data) {
                                    //system.log(JSON.stringify(rec.finalResult(rec), null, 4))

                                    outputContents.push(rec.finalResult(rec))

                                    outputFileContents = JSON.stringify(outputContents)

                                    outputFile = fileName + ".json"

                                    fs.writeFileSync(outputFile, outputFileContents)

                                    rec.free()
                                }

                                wfReadable.on("data", onData)
                                wfReadable.on("end", onEnd)
                            }

                            wfReader.on("format", onFormat)

                            readStream = fs.createReadStream(file, {"highWaterMark": 4096})
                            readStream.pipe(wfReader)

                            async onFinish(err) {
                                model.free()
                            }

                            readStream.on("finish", onFinish)
                        }
                    }
                }

                options = []

                // Get all file names based on glob pattern.
                local glob := Glob(inputFile, options, processFiles)
            } else {
                system.log(maiavoice.appTitle + " Command Line Interface (CLI)")
                system.log("Usage: " + maiavoice.appName + " [options] [input file] [--] [arguments]")
            }
        }
    }
}

// Run the MaiaApp code if this script has been invoked
// from the command line.
if (core.type(process) != "undefined") {
    maiavoice.run()
}